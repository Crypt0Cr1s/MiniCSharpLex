 
   
/* ---------------Seccion de declaraciones preliminares--------------------*/
package minicsharplex;

/* Import the class java_cup.runtime.*  */
import java_cup.runtime.*;
import java.io.FileReader;
import java.util.ArrayList;
import minicsharplex.numero;
/* Codigo del parser, se copia integramente a la clase final.
    Agregamos el manejo de errores. */
parser code {:
    
	 /* Reporte de error encontrado. */
    int errorCounter=0;
    public void syntax_error(Symbol s){
      errorCounter++;

        report_error("Error de Sintaxis recuperable No" + errorCounter + ".Linea: " + (s.left+1) + "Columna: " + (s.right+1) + ". Texto: \""+ s.value + "\"", null);
    }
   
    /* Cuando se encuentra un error de donde el sistema no puede
        recuperarse, se lanza un error fatal. Se despliega el mensaje
        de error y se finaliza la ejecucion. */
    public void unrecovered_Syntax_error(Symbol s)throws java.lang.Exception {
        report_error("Error de sintaxis no recuperable No" + errorCounter + ".Linea: " + (s.left+1) + "Columna: " + (s.right+1) + ". Texto: \""+ s.value + "\"",null);
    }

    public void sem_error(String lexeme, String message) {
      errorCounter++;
      System.err.println("Semantic error");
      System.err.println("  "+ errorCounter + "==> " + message + ": "+ lexeme + "\n");
  }

    
    
    
    /* Metodo main para garantizar la ejecucion del analizador
       lexico y sintactico, ademas que se pase como parametro la tabla
       de simbolos correspondiente. */

   
   
   
:};   

   
/* ------------Declaracion de simbolos terminales y no terminales---------- */
   
/* Terminales (tokens obtenidos por el analizador lexico).  

   Terminales que no tienen un valor son listados primero, los terminales que
   tienen un valor como los enteros son listados en la segunda o demas lineas. */
terminal Object INCLUDE,ARCHIVO,PLUS, MINUS, MULTI, DIV, MOD, MINOR, MINOREQ ,MAJOR, MAJOREQ, ASIGNATION, EQUAL, NOTEQUAL, AND, OR, ADMIRATION, SEMICOLON, COMMA, POINT, CORCHIZQ, CORCHDER, PARENIZQ, PARENDER, LLAVEIZQ, LLAVEDER, CORCHDOBLE, VOID, INT, DOUBLE, BOOL, STRING, CLASS, INTERFACE, NULL, THIS, EXTENDS, IMPLEMENTS, FOR, WHILE, IF, ELSE, RETURN, BREAK, NEW, NEWARRAY, PRINT, READINT, READLINE, MALLOC, HEX, ENTERO, BOOLEAN, CADENA, DOBLE, IDENTIFIER, GETBYTE, SETBYTE;

   
/* No terminales usados en la seccion gramatical. 

   Primero se lista los no terminales que tienen un valor Object y despues
   se lista los no terminales que tienen un entero. Un Object se refiere a que
   no tienen tipo, pudiendo ser entero o String. */
non terminal Object     Start,Include, Program, Decl, VariableDecl, Variable, Type, Expr1,FunctionDecl, FuncCont, Formals, ClassDecl, Ext, Imp, ClassField, ImpID, Field, InterfaceDecl, InterfaceProt,  Prototype, ProtCont, StmtBlock, StmtCont,StmtEnd, Stmt, IfStmt, StmtExpr, ElseIfStmt, WhileStmt, ForStmt, ReturnStmt, BreakStmt, PrintStmt, ExprPrint,  Expr, LValue, Call, CallCont, LibCall, Actuals,  Constant;

   

/* -------------Seccion de predencia y asociacion de los terminales----------- */
precedence left PLUS, MINUS;
precedence left MULTI, DIV,  MOD;
precedence left MINOR, MINOREQ, MAJOR, MAJOREQ, ASIGNATION, EQUAL, NOTEQUAL, AND, OR, ADMIRATION;
precedence left ELSE;
precedence left IDENTIFIER;

/* ------------------- Seccion de la gramatica ------------------------ */
   
Start ::=Program ;

Program ::= Program Decl
| /* ε */ ;



Decl ::= VariableDecl
| FunctionDecl
| ClassDecl
| InterfaceDecl
|error SEMICOLON
|error LLAVEDER ;

VariableDecl ::= Variable SEMICOLON ;

Variable ::= Type:t IDENTIFIER:n{:
            if (!Env.put(n,t)) System.out.println(" La variable " + n + " se encuentra declarara en el ambito actual"); 
                    
            :};

Type ::= INT{: RESULT = "int";:}
| DOUBLE {: RESULT = "double"; :}
| BOOL {: RESULT = "bool"; :}
| STRING {: RESULT = "string"; :}
| IDENTIFIER:n1 {: RESULT = n1.toString(); :}
| Type CORCHDOBLE ;

FunctionDecl ::= Type FuncCont
| VOID FuncCont;

FuncCont ::= IDENTIFIER PARENIZQ Formals PARENDER StmtBlock ;

Formals ::= Formals COMMA Variable
| Variable
| /* ε */ ;

ClassDecl ::= CLASS IDENTIFIER:a Ext:b {:
    if(b == null)
    {
        if(Env.putClass(a.toString(),null,"class") != 0)
        {
            System.out.println("  La clase " + a + " ya ha sido declarada en el ambito actual");
        }
    }
    else
    {
        switch(Env.putClass(a.toString(),b.toString(),"class"))
        {
            case 1:
            System.out.println("  La clase " + a + " ya ha sido declarada en el ambito actual");
            break;

            case 2:
            System.out.println("  La clase " + b + " no existe en el ambito actual");
            break;                
        } 
    }
:}
Imp:c
{:

    if(c == null)
    {
        
    }
    else
    {   
        boolean R = Env.getInterfaces(c.toString());

        if(R == false)
        {
            System.out.println("  La interfaz " + c + " no existe en el ambito actual");
        }
    }
:} 
LLAVEIZQ ClassField LLAVEDER{:
      System.out.print("  Saliendo de la clase: "+a);
      Env.pop();
:};

Ext ::= EXTENDS IDENTIFIER:a {: RESULT = a; :}
| /* ε */ ;

Imp ::= IMPLEMENTS ImpID:a {: RESULT = a; :}
| /* ε */ ;

ImpID ::= ImpID COMMA IDENTIFIER:e {: RESULT = e; :}
| IDENTIFIER:e {: RESULT = e; :} ;

ClassField ::= ClassField Field
| /* ε */ ;

Field ::= VariableDecl
| FunctionDecl ;

InterfaceDecl ::= INTERFACE IDENTIFIER:n1 {: if(Env.putClass(n1.toString(),null,null) != 0) System.out.println(" La interfaz " + n1 + " ya ha sido declarada en el ambito actual"); :} LLAVEIZQ InterfaceProt LLAVEDER ;

InterfaceProt ::= InterfaceProt Prototype
| /* ε */ ;

Prototype ::= Type ProtCont
| VOID ProtCont;

ProtCont ::= IDENTIFIER PARENIZQ Formals PARENDER SEMICOLON ;

StmtBlock ::= LLAVEIZQ StmtCont LLAVEDER 
            ;
StmtCont ::= StmtEnd StmtCont
        |;

StmtEnd ::=VariableDecl
|Stmt;

Stmt ::= StmtExpr SEMICOLON
| IfStmt
| WhileStmt
| ForStmt
| BreakStmt
| ReturnStmt
| PrintStmt
| StmtBlock ;

StmtExpr ::= Expr
| /* ε */ ;

IfStmt ::= IF PARENIZQ Expr PARENDER Stmt ElseIfStmt ;

ElseIfStmt ::= ELSE Stmt
| /* ε */ ;

WhileStmt ::= WHILE PARENIZQ Expr PARENDER Stmt ;

ForStmt ::= FOR PARENIZQ StmtExpr SEMICOLON Expr SEMICOLON StmtExpr PARENDER Stmt ;

ReturnStmt ::= RETURN StmtExpr SEMICOLON ;

BreakStmt ::= BREAK SEMICOLON ;

PrintStmt ::= PRINT PARENIZQ ExprPrint PARENDER SEMICOLON ;

ExprPrint ::= ExprPrint COMMA Expr
| Expr ;

Expr ::= LValue:v ASIGNATION Expr:e{: 
    Env.Validar(v.toString(), e.toString());
:} 
| Constant:a {:RESULT=a;:}
| LValue:a {: RESULT = a; :}
| THIS
| Call 
| PARENIZQ Expr PARENDER
| Expr1:e PLUS Expr:f {: RESULT = e + "+" + f; :}
| Expr1:e MINUS Expr:f {: RESULT = e + "-" + f; :}
| Expr1:e MULTI Expr:f {: RESULT = e + "*" + f; :}
| Expr1:e DIV Expr:f {: RESULT = e + "/" + f; :}
| Expr MOD Expr
| MINUS Expr
| Expr MINOR Expr
| Expr MINOREQ Expr
| Expr MAJOR Expr
| Expr MAJOREQ Expr
| Expr EQUAL Expr
| Expr NOTEQUAL Expr
| Expr AND Expr
| Expr OR Expr
| ADMIRATION Expr
| NEW PARENIZQ IDENTIFIER PARENDER
| NEWARRAY PARENIZQ Expr COMMA Type PARENDER
| READINT PARENIZQ PARENDER
| READLINE PARENIZQ PARENDER
| MALLOC PARENIZQ Expr PARENDER ;

Expr1::=Constant:e{: RESULT = e; :}
|IDENTIFIER:f {: RESULT = f; :} ;

LValue ::= IDENTIFIER:n {: RESULT = n; :}
| Expr POINT IDENTIFIER
| Expr CORCHIZQ Expr CORCHDER ;

Call ::= IDENTIFIER PARENIZQ Actuals PARENDER
| Expr POINT IDENTIFIER CallCont
| Expr POINT LibCall CallCont ;

CallCont ::= PARENIZQ Actuals PARENDER ;

LibCall ::= GETBYTE PARENIZQ Expr PARENDER 
| SETBYTE PARENIZQ Expr COMMA Expr PARENDER ;

Actuals ::= ExprPrint
| /* ε */ ;

Constant ::= ENTERO:e {: RESULT = e; :}
| DOBLE:e {: RESULT = e; :}
| BOOLEAN
| CADENA:e {: RESULT = e; :}
| NULL 
| HEX; 